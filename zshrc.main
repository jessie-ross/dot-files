source ~/.env

## Completion ##

setopt AUTO_LIST
setopt AUTO_MENU
setopt NONOMATCH
setopt COMPLETEINWORD EXTENDEDGLOB

autoload -Uz compinit && compinit
zstyle ':completion:*' menu select
zstyle ':completion:*' list-colors ''



## History ##
HISTFILE=~/.zsh_history 
HISTSIZE=1500 # history items kept internally
SAVEHIST=1500 # history items saved

setopt SHARE_HISTORY # share history between sessions
setopt HIST_FCNTL_LOCK # use OS file locking
setopt HIST_IGNORE_SPACE # Don't record commands entered starting with a space
setopt HIST_IGNORE_DUPS # don't store immediate dups
setopt HIST_NO_STORE # don't store fc/history commands
setopt HIST_REDUCE_BLANKS # canonicalize whitespace
setopt HIST_VERIFY # pause to edit ! item

stty -ixon # Disable flow-control so C-s works


## Prompt ##

autoload -Uz colors && colors
autoload -Uz promptinit && promptinit && prompt bart
PROMPT="%189>..>%{%F{red}%}%n@%m%b%f%k%9(v. . %{%F{blue}%}%(?.[.%20(?.[%U.%S[))%7v%(?.].%20(?.%u].]%s))%b%f%k )%{%F{default}%}%8~%b%f%k%<<%8v%174(l. . %{%F{default}%}%D%b%f%k)%182(l.. %{%F{red}%}%@%b%f%k)%9(v.
%{%F{blue}%}%(?.[.%20(?.[%U.%S[))%7v%(?.].%20(?.%u].]%s))%b%f%k.)
%# "

autoload -z edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line


## Navigation ##

setopt AUTO_PUSHD
setopt CDABLE_VARS



## Extras ##

autoload -Uz zmv # Batch move and rename 
alias zmv='noglob zmv'



## Custom functions and aliases ##

function gwta () {
	# Create a git worktree under /repo-name.worktrees/ and pushd to it.
	# Expects a bare repo to exist at /repo-name.worktrees/bare-repo.git
	NEW_BRANCH=$1
	EXISTING_BRANCH=${2:-origin/master}
	WORKTREE_ROOT=$(pwd | sed 's/\(^.*\.worktrees\).*/\1/')
	BARE_DIR="${WORKTREE_ROOT}/bare-repo.git"
	if [ -d "${BARE_DIR}" ] 
	then
		pushd "${BARE_DIR}" > /dev/null
		git worktree add -b "$NEW_BRANCH" "${WORKTREE_ROOT}/${NEW_BRANCH}" "$EXISTING_BRANCH" 
		popd > /dev/null
		pushd "${WORKTREE_ROOT}/${NEW_BRANCH}" 
		if [ -d "${WORKTREE_ROOT}/${EXISTING_BRANCH}/node_modules" ]
		then
			ln -s "${WORKTREE_ROOT}/${EXISTING_BRANCH}/node_modules" .
		fi
	else
		echo Cannot find root bare directory
	fi
}
function napropos () {
	npm run | grep $1 -A 1
}
alias nr='npm run'
alias g='git'
alias v='nvim'
alias l='ls'
alias la='ls -A'
alias ll='ls -lAh'
alias readyit='time ( npm run format && npm run lint:ts:fix && npm run test:all ) ; say -v Karen task finished'
alias testit='time npm run test:all; say -v Karen task finished'
alias format='time ( npm run format && npm run lint:ts:fix ) ; say -v Karen task finished'

export RIPGREP_CONFIG_PATH="${HOME}/.ripgreprc"


# Change directory to what Finder is looking at:
# Via https://twitter.com/QuinnyPig/status/1473112091808591874
cdf() {
    target=`osascript -e 'tell application "Finder" to get POSIX path of (target of front Finder window as text)'`
    cd "$target"
}

alias vsetup='( cd ~/code-personal/dot-files/ && v ./setup.sh && ./setup.sh )'
alias vzshrc='v ~/code-personal/dot-files/zshrc.main && source ~/code-personal/dot-files/zshrc.main'
alias vzshrcl='v ~/.zshrc && source ~/.zshrc'


function op-open-and-fill() (
    local VAULT_UUID="$1"
    local ITEM_UUID="$2"
    local URL="$3"
    local SHA256_URL="$(echo -n "$URL" | sha256sum | awk '{print $1;}')"

    open "onepassword7://open_and_fill/$VAULT_UUID/$ITEM_UUID/$SHA256_URL"
    # 1P needs a bit of time to do it's thing:
    sleep 3
)


function op-ssh-add() (
	# Requires $OP_SESSION_my to be set
	
	AUTH_TIMEOUT="${1:-1h}"
	SSH_KEY_PATH="$2"
	OP_ITEM_UUID="$3"
	
	# This is all required to provide a password into SSH_ASKPASS
	TEMP_OP_PW_SCRIPT_PATH=$(mktemp)
	cat <<EOF > "$TEMP_OP_PW_SCRIPT_PATH"
#!/bin/bash
OP_SESSION_my="${OP_SESSION_my}" op get item "${OP_ITEM_UUID}" --fields password
EOF

	chmod u+x "$TEMP_OP_PW_SCRIPT_PATH"

	DISPLAY=1 SSH_ASKPASS="${TEMP_OP_PW_SCRIPT_PATH}" ssh-add -t "${AUTH_TIMEOUT}" "${SSH_KEY_PATH}" < /dev/null

	rm "$TEMP_OP_PW_SCRIPT_PATH"
)
